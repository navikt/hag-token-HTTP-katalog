val kotlin_version: String by project
val logback_version: String by project
val ktor_version: String by project

plugins {
    application
    kotlin("jvm") version "2.0.0"
    id("io.ktor.plugin") version "2.3.12"
    id("com.github.johnrengelman.shadow") version "8.1.1"
    id("maven-publish")
}

group = "no.nav.helsearbeidsgiver"
version = "0.0.1"

application {
    mainClass.set("io.ktor.server.netty.EngineMain")

//    mainClass.set("no.nav.helsearbeidsgiver.ApplicationKt")

    val isDevelopment: Boolean = project.ext.has("development")
    applicationDefaultJvmArgs = listOf("-Dio.ktor.development=$isDevelopment")
}

repositories {
    val githubPassword: String by project

    mavenCentral()

    maven {
        setUrl("https://maven.pkg.github.com/navikt/*")
        credentials {
            username = "x-access-token"
            password = githubPassword
        }
    }
}

dependencies {
    implementation("io.ktor:ktor-server-core-jvm")
    implementation("io.ktor:ktor-server-netty")
    implementation("ch.qos.logback:logback-classic:$logback_version")
    implementation("io.ktor:ktor-server-core")
    implementation("io.ktor:ktor-server-config-yaml")
    testImplementation("io.ktor:ktor-server-test-host")
    testImplementation("org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version")

    implementation("no.nav.helsearbeidsgiver:maskinporten-client:0.2.1.1-SNAPSHOT")
    implementation("io.github.cdimascio:dotenv-kotlin:6.4.2")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.8.1")

    implementation("io.kubernetes:client-java:18.0.1")

    implementation("io.ktor:ktor-client-core:$ktor_version")
    implementation("io.ktor:ktor-client-cio:$ktor_version")

    implementation("io.ktor:ktor-server-cors:$ktor_version")
    implementation("io.ktor:ktor-client-apache5:$ktor_version")
}

tasks.register("start") {
    group = "application"
    description = "Runs the start script"

    doFirst {
        file("./release/bin/start").setExecutable(true)
    }

    doLast {
        exec {
            commandLine("./release/bin/start")
        }
    }
}

task("safe-commit") {

    println("⏳ Legger til pre-commit script ...")
    doLast {
        exec {
            commandLine("cp", "./.scripts/pre-commit", "./.git/hooks")
        }
    }
    println("✅ Lagt til no-token sikkerhet sjekk i pre-commit")
}
